plugins {
    id 'java-library'
    id "io.qameta.allure" version "2.9.6"
    id "io.freefair.lombok" version "8.6"
}
repositories {
    mavenCentral()
}
def allureVersion = "2.28.0",
    selenideVersion = '7.8.1',
    appiumVersion = "8.2.1",
    junitVersion = "5.12.1",
    slf4jVersion = "2.0.16",
    jacksonVersion = "2.14.0",
        versionWebDriverManager="5.9.2"

group = 'demoApiAndUi'
version = '1.0-SNAPSHOT'

allure {
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
    report {
        version.set(allureVersion)
    }
}

dependencies {
    testImplementation(
            "org.junit.jupiter:junit-jupiter:$junitVersion",
            "io.github.bonigarcia:webdrivermanager:$versionWebDriverManager",
            "com.codeborne:selenide:$selenideVersion",
            "io.rest-assured:rest-assured:5.3.0",
            "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion",
            "io.appium:java-client:$appiumVersion",
            "org.aeonbits.owner:owner:1.0.12",
            "org.assertj:assertj-core:3.23.1",
            "io.qameta.allure:allure-selenide:$allureVersion",
            "io.qameta.allure:allure-rest-assured:$allureVersion",
            "org.aspectj:aspectjweaver:1.9.22",
            "com.github.javafaker:javafaker:1.0.2",
            "org.slf4j:slf4j-simple:$slf4jVersion")
}
tasks.withType(JavaExec) {
    jvmArgs '-Dfile.encoding=UTF-8'
    systemProperty "file.encoding", "UTF-8"
}

tasks.withType(Test) {
    systemProperty "file.encoding", "UTF-8"
}

tasks.withType(Test) {
    systemProperties(System.getProperties())
    useJUnitPlatform {
        if (System.getProperty("tag") != null && !System.getProperty("tag").isEmpty()) {
            includeTags System.getProperty("tag")
        }
    }

    if (System.getProperty("threads") != null) {
        systemProperties += [
                'junit.jupiter.execution.parallel.enabled'                 : true,
                'junit.jupiter.execution.parallel.mode.default'            : 'concurrent',
                'junit.jupiter.execution.parallel.mode.classes.default'    : 'concurrent',
                'junit.jupiter.execution.parallel.config.strategy'         : 'fixed',
                'junit.jupiter.execution.parallel.config.fixed.parallelism': System.getProperty("threads").toInteger()
        ]
    }

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}